eval "$_z4h_opt"

{
  [[ -n $TTY && ( -n $CONTINUOUS_INTEGRATION || -z $MC_SID ) ]] || return
  local repo
  repo="${HOMEBREW_REPOSITORY:-$(command brew --repository 2>/dev/null)}" || return
  [[ -n $repo/Library/Taps/*/*/cmd/brew-command-not-found-init(|.rb)(#q.N) ]] || return

  function command_not_found_handler() {
    eval "$_z4h_opt"
    local fd fname
    if zstyle -s :z4h:command-not-found to-file fname &&
       [[ -w $fname || ! -e $fname && -w ${fname:h} ]]; then
      fd=3
    else
      fd=2
      fname=/dev/null
    fi
    {
      if (( $#functrace >= 2 )); then
        print -r -- "$functrace[1]: command not found: $1"
      else
        local msg
        if msg="$(command brew which-formula --explain $1 2>/dev/null)" && [[ -n $msg ]]; then
          print -r -- $msg
        else
          print -r -- "zsh: command not found: $1"
        fi
      fi
    } 3>$fname >&$fd
    return 127
  }
} always {
  unfunction -- -z4h-brew-command-not-found
  autoload -Uz -- -z4h-brew-command-not-found
}
